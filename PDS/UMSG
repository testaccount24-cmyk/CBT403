/* Rexx UMSG
 |
 |  Data Set   : RE03063.MESAJ.PDS(UMSG)
 |  Name       : Ugur Cilesiz
 |  Date       : 06.08.2002
 |  Parameter  :
 |  Description: Display Information For DFSMS (hsm, dfp and dss)
 |               Messages
 */

If Initialise() <> 0
   Then Exit 8

Arg Mesaj

Mesaj = Strip(Mesaj)

Address ISPEXEC "CONTROL ERRORS RETURN"

If Pos("ARC",Mesaj) ¬= 0 | Pos("ADR",Mesaj) ¬= 0 | Pos("IDC",Mesaj) ¬= 0
   Then Signal  FindTheMessage

Main:
Mesaj = 'ARCxxxx'
Address ISPEXEC "ADDPOP ROW(03) COLUMN(04)"
Address ISPEXEC "DISPLAY PANEL("Pan_Main")"

If Rc = 12 | Rc = 20
   Then Do
        ZEDSMSG = 'Panel error|'
        ZEDLMSG = 'Display Panel Error Rc 'Rc
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Call Exitus
        End

If ZCMD = Trace
   Then Do
        ZCMD = ' '
        Trace ?r
        End

If Rc = 8
   Then Do
        Address ISPEXEC "REMPOP"
        Call Exitus
        End

Address ISPEXEC "REMPOP"

FindTheMessage:

If Pos("*",Mesaj) <> 0
   Then Do
        MessageMember = Mesaj
        Address ISPEXEC ,
        "LMINIT DATAID(LibDD) DATASET('"Msg_DSN"')",
        "ENQ(SHRW)  ORG(LIBORG)"

        Address ISPEXEC ,
        "LMOPEN DATAID("LibDD") OPTION(INPUT) LRECL(LIBRECL)",
        "RECFM(LIBRECFM) ORG(LIBORG)"

        If Rc <> 0
           Then Signal Exitus

        Display:
        Address ISPEXEC ,
        "LMMDISP DATAID("LibDD")  OPTION(DISPLAY) COMMANDS(ANY)",
        "MEMBER("MessageMember")"
        If Rc > 0
           Then If Rc = 8
                   Then Signal Main
                   Else Signal Exitus
        Do While Rc = 0
           ZLMEMBER = Strip(ZLMEMBER)
           If Pos(ZLLCMD,"SB") = 0
              Then Signal Display
           If ZLLCMD = "B"
              Then ISPF = "BROWSE"
           If ZLLCMD = "S"
              Then ISPF = "BROWSE"
        Address ISPEXEC ,
        "LMINIT DATAID(EditDD) DATASET('"Msg_DSN"')",
        "ENQ(SHRW)  ORG(LIBORG)"
        Address ISPEXEC ISPF" DATAID("EditDD") MEMBER("ZLMEMBER")"
        Address ISPEXEC "LMFREE DATAID("EditDD")"
        Address ISPEXEC "LMMDISP DATAID("LibDD")  OPTION(GET)"
        Signal Display
        End /* Do While */
        End /* If Pos(  */

   Else Do
        If Pos("ADR",Mesaj) ¬= 0
           Then MessageMember = Strip(Substr(Mesaj,1,7))
           Else MessageMember = Strip(Substr(Mesaj,1,8))

        Address ISPEXEC "BROWSE DATASET('"Msg_DSN"("MessageMember")')"

        If Rc > 0
           Then Do
                ZEDSMSG = 'No Message|'
                ZEDLMSG = 'No Message Available with Id = ' ||Mesaj
                Address ISPEXEC "SETMSG MSG(ISRZ001)"
                Call Main
                End
        End

Call Main

Exitus:
   Address ISPEXEC "LMCLOSE DATAID("LibDD")"
   Address ISPEXEC "LMFREE DATAID("LibDD")"
  ExitRc = LIBDEF_Libraries(AllDDNs)
Exit

Initialise:

Pan_DSN     = 'RE03063.MESAJ.PDS'
Pan_DDn     = 'ISPPLIB'
Pan_Main    = 'MESAJP'

Msg_DSN     = 'RE03063.MESAJ.PDS'
Msg_DDn     = 'ISPMLIB'

AllDDNs     = Pan_DDn  Msg_DDn
AllDSNs     = Pan_DSN  Msg_DSN

If LIBDEF_Libraries(AllDDNs,AllDSNs) <> 0
   Then Return 8

Return 0

LIBDEF_Libraries:
Parse Arg DDNames,DSNames


Do i = 1 To Words(DDNames)
   If DSNames = ''
      Then Address ISPEXEC "LIBDEF" Word(DDNames,i)
      Else Do
           Address ISPEXEC ,
                   "LIBDEF" Word(DDNames,i) "DATASET ID('"Word(DSNames,i)"')",
                   "STACK"
           If Rc <> 0
              Then Do
                   Say 'Rc='Rc 'from LIBDEF of' Word(DSNames,i) ,
                       'To' Word(DDNames,i)
                   Call LIBDEF_Libraries Subword(DDNames,i)
                   Leave Rc=8
                   End
           End

End

Return Rc
